!***************************************************************************
! footprint_handle.f90
! --------------------
! Copyright (C) 2007-2011, Eco2s team, Gerardo Fratini
! Copyright (C) 2011-2014, LI-COR Biosciences
!
! This file is part of EddyPro (TM).
!
! EddyPro (TM) is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! EddyPro (TM) is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License
! along with EddyPro (TM).  If not, see <http://www.gnu.org/licenses/>.
!
!***************************************************************************
!
! \brief       Hub and implementation to several cross-wind \n
!              integrated footprint models
! \author      Gerardo Fratini
! \note
! \sa
! \bug
! \deprecated
! \test
! \todo
!***************************************************************************
subroutine FootprintHandle(lEx)
    use m_fx_global_var
    implicit none
    !> In/out variables
    type(ExType), intent(in) :: lEx

    foot_model_used = Meth%foot(1:len_trim(Meth%foot))
    !> If Kljun model was chosen, but conditions are outside those stated at Pag. 512 of the paper
    !> shift to Kormann and Meixner model.

    if (foot_model_used == 'kljun_04' .and. &
        (lEx%var(w) <= 0d0 .or. lEx%ustar < kj_us_min .or. &
        lEx%zL < kj_zL_min .or. lEx%zL > kj_zL_max .or. lEx%instr(sonic)%height < 1d0)) &
        foot_model_used = 'kormann_meixner_01'
    select case(foot_model_used)
        case('kljun_04')
            call Kljun04(lEx)
        case('kormann_meixner_01')
            call KormannMeixner01(lEx)
        case('hsieh_00')
            call Hsieh00(lEx)
    end select
end subroutine FootprintHandle

!***************************************************************************
!
! \brief       Footprint esitmations based on Kljun et al. (2004, BLM)
! \author      Gerardo Fratini
! \note
! \sa
! \bug
! \deprecated
! \test
! \todo
!***************************************************************************
subroutine Kljun04(lEx)
    use m_fx_global_var
    implicit none
    !> In/out variables
    type(ExType), intent(in) :: lEx
    !> local variables
    real(kind = dbl) :: sigma_w
    real(kind = dbl) :: xstarmax
    real(kind = dbl) :: xstar
    real(kind = dbl) :: af, bb, ac, ad
    real(kind = dbl) :: a, b, c, d
    real(kind = dbl) :: zm
    real(kind = dbl) , external :: gammln

    real(kind = dbl) :: L(96)

    !> Values of L' for varying R (percentage of footprint) for b = 3.70 (see Fig. A1 in Kljun et al. 2004)
    data L(1:96) / 0.000000d0, 0.302000d0, 0.368000d0, 0.414000d0, 0.450000d0, 0.482000d0, 0.510000d0, &
        0.536000d0, 0.560000d0, 0.579999d0, 0.601999d0, 0.621999d0, 0.639999d0, 0.657998d0, 0.675998d0, &
        0.691998d0, 0.709998d0, 0.725998d0, 0.741997d0, 0.755997d0, 0.771997d0, 0.785997d0, 0.801997d0, &
        0.815996d0, 0.829996d0, 0.843996d0, 0.857996d0, 0.871996d0, 0.885995d0, 0.899995d0, 0.911995d0, &
        0.925995d0, 0.939995d0, 0.953995d0, 0.965994d0, 0.979994d0, 0.993994d0, 1.005994d0, 1.019994d0, &
        1.033994d0, 1.045993d0, 1.059993d0, 1.073993d0, 1.085993d0, 1.099993d0, 1.113993d0, 1.127992d0, &
        1.141992d0, 1.155992d0, 1.169992d0, 1.183992d0, 1.197991d0, 1.211991d0, 1.225991d0, 1.239991d0, &
        1.253991d0, 1.269991d0, 1.283990d0, 1.299990d0, 1.315990d0, 1.329990d0, 1.345990d0, 1.361989d0, &
        1.379989d0, 1.395989d0, 1.411989d0, 1.429989d0, 1.447988d0, 1.465988d0, 1.483988d0, 1.501988d0, &
        1.521987d0, 1.539987d0, 1.559987d0, 1.581987d0, 1.601986d0, 1.623986d0, 1.647986d0, 1.669985d0, &
        1.693985d0, 1.719985d0, 1.745984d0, 1.773984d0, 1.801984d0, 1.831983d0, 1.863983d0, 1.895983d0, &
        1.931982d0, 1.969982d0, 2.009982d0, 2.053984d0, 2.101986d0, 2.153988d0, 2.211991d0, 2.279994d0, 2.355998d0 /

    !> Initialization to error
    Foot = FootType(error, error, error, error, error, error, error)

    !> Height above displacement height
    zm = lEx%instr(sonic)%height - lEx%disp_height

    !> Check on retrieved parameters
    if (zm < 1d0 .or. lEx%rough_length <= 0d0) return

    !> Calculate a, b, c, d, depending only on z0 (Eq. 13-16 in Kljun et al. 2004)
    af = 0.175d0
    bb = 3.418d0
    ac = 4.277d0
    ad = 1.685d0
    b  = 3.69895d0
    a = af / (bb - dlog(lEx%rough_length))
    c = ac * (bb - dlog(lEx%rough_length))
    d = ad * (bb - dlog(lEx%rough_length))

    !> Calculate sigma_w
    if (lEx%var(w) >= 0d0) then
        sigma_w = dsqrt(lEx%var(w))
    else
        sigma_w = error
    end if

    if (sigma_w == error .or. lEx%ustar < kj_us_min .or. lEx%zL < kj_zL_min .or. lEx%zL > kj_zL_max) then
        Foot = FootType(error, error, error, error, error, error, error)
    else
        !> Calculate location of peak influence
        xstarmax = c - d
        Foot%peak = xstarmax * zm *(sigma_w / lEx%ustar)**(-0.8d0)

        !> Calculate offset from tower: location of 1% contribution
        xstar = L(2) * c - d
        Foot%offset = xstar * zm *(sigma_w / lEx%ustar)**(-0.8d0)

        !> Calculate distances including increasing percentages of the footprint
        xstar = L(11) * c - d
        Foot%x10 = xstar * zm *(sigma_w / lEx%ustar)**(-0.8d0)
        xstar = L(31) * c - d
        Foot%x30 = xstar * zm *(sigma_w / lEx%ustar)**(-0.8d0)
        xstar = L(51) * c - d
        Foot%x50 = xstar * zm *(sigma_w / lEx%ustar)**(-0.8d0)
        xstar = L(71) * c - d
        Foot%x70 = xstar * zm *(sigma_w / lEx%ustar)**(-0.8d0)
        xstar = L(91) * c - d
        Foot%x90 = xstar * zm *(sigma_w / lEx%ustar)**(-0.8d0)
    end if
end subroutine Kljun04

!***************************************************************************
!
! \brief       Footprint esitmations based on Kormann and Meixner, 2001
! \author      Gerardo Fratini
! \note
! \sa
! \bug
! \deprecated
! \test
! \todo
!***************************************************************************
subroutine KormannMeixner01(lEx)
    use m_fx_global_var
    implicit none
    !> In/out variables
    type(ExType), intent(in) :: lEx
    !> local variables
    integer :: i
    real(kind=dbl) :: n
    real(kind=dbl) :: u_mean
    real(kind=dbl) :: phi_m
    real(kind=dbl) :: phi_c
    real(kind=dbl) :: psi_m
    real(kind=dbl) :: eta
    real(kind=dbl) :: key
    real(kind=dbl) :: m
    real(kind=dbl) :: UU
    real(kind=dbl) :: r
    real(kind=dbl) :: mmu
    real(kind=dbl) :: zeta
    real(kind=dbl) :: zm
    real(kind=dbl) :: int_foot
    real(kind=dbl), parameter :: di = 1d0
    logical :: do_offset
    logical :: do10
    logical :: do30
    logical :: do50
    logical :: do70


    !> Initialization to error
    Foot = FootType(error, error, error, error, error, error, error)

    zm = lEx%instr(sonic)%height - lEx%disp_height
    u_mean = lEx%WS
    !> ALTERNATIVE u(z) for z=zm (Monin-Obukhov similarity profile)
    !u_mean = lEx%ustar *(dlog(zm / lEx%rough_length) + psi_m) / vk

    !> Similarity relations (Paulson, 1970)
    if (lEx%zL > 0) then
        phi_m = 1d0 + 5d0 * lEx%zL
        phi_c = phi_m
        psi_m = - 5d0 * lEx%zL
    else
        phi_m = (1d0 - 16d0 * lEx%zL)**(-1d0 / 4d0)
        phi_c = (1d0 - 16d0 * LitePar%zL)**(-1d0 / 2d0)
        eta = (1d0 - 16d0 * lEx%zL)**(1d0 / 4d0)
        psi_m = 2d0 * dlog((1d0 + eta) / 2d0) + dlog((1d0 + eta**2) / 2d0) - 2d0 * datan(eta) + p / 2d0  !< K&M2001
        !psi_m = 0.0954d0 - 1.86d0 * (zm/lEx%L) - 1.07d0 * (zm/lEx%L)**2 - 0.249 * (zm/lEx%L)**3  !< Zhang & Anthes 1983, polynomial interpolation
        !psi_m = dlog(((1 + eta) / 2d0)**2 * (1d0 + eta**2) / 2d0) - 2d0 * datan(eta) + p / 2d0 !< alternative
    end if
    psi_m = -1d0 * psi_m  !< change sign to conform with K&M usage

    !> Intermediate parameters for K&M2001
    !> exponent of the diffusivity power law
    if (lEx%zL > 0) then
        n  = 1d0 / phi_m
    else
        n = (1d0 - 24d0 * lEx%zL) / (1d0 - 16d0 * lEx%zL)
    end if
    !> proportionality constant of the diffusivity power law (Eqs. 11 and 32)
    key  = vk * lEx%ustar * zm / (phi_c * zm**n)

    !> exponent of the wind speed power law
    m = lEx%ustar * phi_m / (vk * u_mean)

    !> proportionality constant of the wind speed power law (Eqs. 11 and 31)
    UU = lEx%ustar * (dlog(zm / lEx%rough_length) + psi_m) / (vk * zm**m)
    UU = u_mean / zm**m
    !> Intermediate parameters
    r = 2d0 + m - n
    mmu = (1 + m) / r
    zeta = UU * zm**r / (r**2 * key)

    !> Footprint according to Kormann and Meixner, 2001
    do_offset = .true.
    do10 = .true.
    do30 = .true.
    do50 = .true.
    do70 = .true.
    int_foot = 0d0

    do i = 1, 10000
        !> Cross-wind integrated 1D function
        int_foot = int_foot + di * (zeta**mmu * dexp(-zeta / (i * di)) / ((i * di)**(1 + mmu) * gamma(mmu)))
        if (do_offset .and. int_foot > 0.01d0) then
            Foot%offset = i * di
            do_offset = .false.
        end if
        if (do10 .and. int_foot > 0.1d0) then
            Foot%x10 = i * di
            do10 = .false.
        end if
        if (do30 .and. int_foot > 0.3d0) then
            Foot%x30 = i * di
            do30 = .false.
        end if
        if (do50 .and. int_foot > 0.5d0) then
            Foot%x50 = i * di
            do50 = .false.
        end if
        if (do70 .and. int_foot > 0.7d0) then
            Foot%x70 = i * di
            do70 = .false.
        end if
        if (int_foot > 0.9d0) then
            Foot%x90 = i * di
            exit
        end if
    end do
    !> Peak value
    Foot%peak = zeta / (1d0 + mmu)
end subroutine KormannMeixner01

!***************************************************************************
!
! \brief       Footprint esitmations based on Hsieh et al. 2000
! \author      Gerardo Fratini
! \note
! \sa
! \bug
! \deprecated
! \test
! \todo
!***************************************************************************
subroutine Hsieh00(lEx)
    use m_fx_global_var
    implicit none
    !> In/out variables
    type(ExType), intent(in) :: lEx
    !> local variables
    integer :: i
    real(kind=dbl) :: a1
    real(kind=dbl) :: DD
    real(kind=dbl) :: PP
    real(kind=dbl) :: fact
    real(kind=dbl) :: p1
    real(kind=dbl) :: zu
    real(kind=dbl) :: z0m
    real(kind=dbl) :: zm
    real(kind=dbl) :: zL
    real(kind=dbl) :: int_foot
    real(kind=dbl), parameter :: di = 5d0
    logical :: do_offset
    logical :: do10
    logical :: do30
    logical :: do50
    logical :: do70


    !> Initialization to error
    Foot = FootType(error, error, error, error, error, error, error)

    zm = lEx%instr(sonic)%height - lEx%disp_height

    !> Model by Hsieh et al. (2000)
    !> Intermediate parameters
    a1 = 0.3d0
    p1 = 0.86d0
    z0m = lEx%rough_length
    zu = zm * (dlog(zm/lEx%rough_length) - 1d0 + lEx%rough_length/zm)
    zL = zu / lEx%L

    !> Parameers D and P in Eq. 17
    DD = 0.97d0
    PP = 1d0
    if (dabs(zL) < 0.04d0) then
        !> Neutral and near neutral conditions
        DD = 0.97d0
        PP = 1d0
    elseif(zL < 0d0) then
        !> Unstable conditions
        DD = 0.28d0
        PP = 0.59d0
    elseif(zL > 0d0) then
        !> Stable conditions
        DD = 2.44d0
        PP = 1.33d0
    end if

    !> Footprint according to Hsieh et al., 2000
    do_offset = .true.
    do10 = .true.
    do30 = .true.
    do50 = .true.
    do70 = .true.
    do i = 1, 10000
        !> Cross-wind integrated 1D function
        fact = DD * zu**PP * dabs(lEx%L)**(1d0 - PP) / (vk**2 * (i * di))
        int_foot = dexp(-fact)
        if (do_offset .and. int_foot > 0.01d0) then
            Foot%offset = i * di
            do_offset = .false.
        end if
        if (do10 .and. int_foot > 0.1d0) then
            Foot%x10 = i * di
            do10 = .false.
        end if
        if (do30 .and. int_foot > 0.3d0) then
            Foot%x30 = i * di
            do30 = .false.
        end if
        if (do50 .and. int_foot > 0.5d0) then
            Foot%x50 = i * di
            do50 = .false.
        end if
        if (do70 .and. int_foot > 0.7d0) then
            Foot%x70 = i * di
            do70 = .false.
        end if
        if (int_foot > 0.9d0) then
            Foot%x90 = i * di
            exit
        end if
    end do

    !> Peak distance
    Foot%peak = DD * zu**PP * dabs(lEx%L)**(1d0 - PP) / (2d0 * vk**2)
end subroutine Hsieh00
